buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:3.0.2'
	}
}

plugins {
  id "org.sonarqube" version "3.5.0.2730"
}

subprojects {
	group = 'org.big.erd'
	//version = '0.1.0-SNAPSHOT'
	
	ext.xtextVersion = '2.27.0'
	ext.sprottyVersion = '0.12.0'
	ext.elkVersion = '0.7.1'
	ext.junit4Version = '4.13'
	ext.junit5Version = '5.8.1'
	
	repositories {
		mavenCentral()
	}

	apply plugin: 'java' 
	dependencies {
		implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	
	sourceCompatibility = '11'
	targetCompatibility = '11'
	
	configurations.all {
		exclude group: 'asm'
	}

	test {
    	finalizedBy jacocoTestReport // report is always generated after tests run
	}
	jacocoTestReport {
    	dependsOn test // tests are required to run before generating the report
	}
	sonarqube {
  		properties {
    		property "sonar.projectKey", "bigER-server"
    		property "sonar.organization", "plglaser"
    		property "sonar.host.url", "https://sonarcloud.io"
  		}
	}
}


